
require:
   quasar ->
      ID, DIRECTORY, symbols
      setup-proxy, deprox, proxy
      current-frame
      current-context
      side-effect
      emit
      register
   .util ->
      task-function
   path
   fs
   chokidar
   minimatch
   mkdirp

provide:
   FileIterator
   FileSystem
   FSNode



;; extract-text-transformer{f} =
;;    method new-f{file, options} =
;;       state = current-context{}??.state
;;       if not state:
;;          throw E.map-file{"Cannot find a renaming function to use."}
;;       res = f.call{@, file.text, file}
;;       match res:
;;          Write? w -> w
;;          String? s ->
;;             Write{state.self{}.rename{file.path, options}, s}
;;          other ->
;;             other
;;    new-f

class FileIterator:
   constructor{@gen} =
      @results = null
   gen [^Symbol.iterator]{} =
      if @results:
         throw E.engage{"Do not run FileIterator twice"}
         @results each x -> yield x
      else:
         results = {}
         @gen each x ->
            results.push{x}
            yield x
         @results = results
   map{fn} =
      [@] each x -> fn{x}
   map-task{fn} =
      t = task-function{fn}
      [@] each x -> t{x}
   for-each{fn} =
      @map{fn}
      null
   for-each-task{fn} =
      @map-task{fn}
      null
   ;; map-file{options = {=}, fn} =
   ;;    t = task-function{fn, extract-text-transformer}
   ;;    [@] each file -> t{file, {=} & options}
   bundle{fn} =
      throw E.todo{}

analyze-path{p >> p.split{"/"}} =
   var i = 0
   p[... -1] each part ->
      if R"\*"? part:
         break
      i += 1
   {base = p.slice{0, i}.join{"/"} or "/"
    rest = p.slice{i}}


;;;;;;;;;;;;;;;;
;; FileSystem ;;
;;;;;;;;;;;;;;;;

class FileSystem:

   constructor{@root-path, @options} =
      [@] &: @options
      @directory-cache = {=}

   get{_p} =
      p = if{_p.starts-with{"/"}, _p, path.join{@root-path, _p}}
      match @directory-cache[p]:
         undefined? ->
            entry = FSNode{p}
            expr-value @directory-cache[p] =
               match @[DIRECTORY]:
                  undefined? ->
                     entry
                  dir ->
                     dir.acquire{entry}
         existing ->
            existing

   find{glob} =
      {=> base, => rest} = analyze-path{glob}
      @get{base}.find{rest.join{"/"}}


class FileSystemProxy < FileSystem:

   constructor{obj, tracks, patch} =
      [@] &: obj.options
      setup-proxy{@, obj, tracks, patch}

   get{key} =
      @[symbols.register-tracks]{key}
      proxy{@[symbols.obj].get{key}
            @[symbols.tracks]
            @[symbols.patch]}




;;;;;;;;;;;;
;; FSNode ;;
;;;;;;;;;;;;

class FSNode:

   constructor{@path} =
      @_files = null
      try:
         stat = fs.lstat-sync{@path}
         @mtime = stat.mtime
         @type =
            if stat.is-directory{}:
               .directory
            elif stat.is-file{}:
               .file
            else:
               .unsupported
      catch e:
         @mtime = new Date{}
         @type = null

   ;;;;;;;;;;;;
   ;; COMMON ;;
   ;;;;;;;;;;;;

   stat{} =
      @mtime
      fs.lstat-sync{@path}

   exists{} =
      @type !== null

   older{other} =
      @mtime.value-of{} < other.mtime.value-of{}

   more-recent{other} =
      @mtime.value-of{} > other.mtime.value-of{}

   root{} =
      @[DIRECTORY]??[symbols.associated-state]??.get{}

   ;; touch{} =
   ;;    @mtime = Date{}

   ;;;;;;;;;;;;;;;;;;;;
   ;; FILE INTERFACE ;;
   ;;;;;;;;;;;;;;;;;;;;

   read{} =
      if @type === .file:
         @mtime
         emit{.read-file, @path}
         fs.read-file-sync{@path}
      else:
         throw E.engage.read{'read: {@path} is not a file'}

   write{contents} =
      side-effect{#write{@path, contents}}

   ;;;;;;;;;;;;;;;;;;;
   ;; DIR INTERFACE ;;
   ;;;;;;;;;;;;;;;;;;;

   get-files{} =
      if @_files === null:
         @_files = Object.create{null}
         match @type:
            null? ->
               mkdirp.sync{@path}
               @type = .directory
            .file ->
               throw E.not-directory{'"{@path}" is not a directory'}
            else ->
               pass
         match @[DIRECTORY]:
            undefined? ->
               fs.readdir-sync{@path} each filename ->
                  full-path = path.join{@path, filename}
                  @_files[filename] = FSNode{full-path}
            dir ->
               fs.readdir-sync{@path} each filename ->
                  full-path = path.join{@path, filename}
                  @_files[filename] = dir.acquire{FSNode{full-path}}
         @_files
      else:
         @_files

   find{glob} =
      gen g{} =
         match glob.split{"/"}:
            {""} ->
               yield [@]
            {"**", *rest} ->
               yield all @find{rest.join{"/"}}
               [@] each entry when entry.type === .directory ->
                  yield all entry.find{glob}
            {minimatch.make-re! x, *rest} ->
               @keys{} each
                  x? name ->
                     entry = @get{name}
                     if rest == {}:
                        yield entry
                     elif entry.type === "directory":
                        yield all entry.find{rest.join{"/"}}
                  else ->
                     continue
      FileIterator{g{}}

   get{key} =
      @get-files{}
      match key.split{"/"}:
         {file} ->
            @_files[file] or FSNode{path.join{@path, file}}
         {base, *rest} ->
            @get{base}.get{rest.join{"/"}}

   set{String? key or Number? key, value} =
      @get-files{}
      @_files[key] = value

   .delete{key} =
      @get-files{}
      delete @_files[key]

   .keys{} =
      keys{@get-files{}}

   entries{} =
      keys{@get-files{}} each k -> {k, @get{k}}

   map{f} = @find{"*"}.map{f}
   for-each{f} = @find{"*"}.for-each{f}

   gen [^Symbol.iterator]{} =
      @entries{} each {k, v} -> yield v



;; FILE PROPERTIES

Object.define-property{FSNode.prototype, .contents} with {
   method get{} = @read{}
}

Object.define-property{FSNode.prototype, .text} with {
   method get{} = @read{}.to-string{}
}



class FSNodeProxy < FSNode:

   constructor{@path, obj, tracks, patch} =
      setup-proxy{@, obj, tracks, patch, FSNodeTrack}

   _prox{x} =
      proxy{x, @[symbols.tracks], @[symbols.patch]}

   touch{} =
      @[symbols.register-patch]{"//mtime"} with
         #update{new Date{}}

   touch-directory{} =
      @[symbols.register-patch]{"//directories"} with
         #update{true}

   root{} =
      @[symbols.obj]??[DIRECTORY]??[symbols.associated-state]??.get{}

   find{glob} =
      gen g{} =
         match glob.split{"/"}:
            {""} ->
               yield [@]
            {"**", *rest} ->
               yield all @find{rest.join{"/"}}
               @[symbols.register-tracks]{"//directories"}
               [@[symbols.obj]] each entry when entry.type === .directory ->
                  yield all @_prox{entry}.find{glob}

            {orig and minimatch.make-re! x, *rest} ->
               @[symbols.register-tracks]{'//:{orig}'}
               entries = @[symbols.obj].entries{} each {k, v} ->
                  {k, proxy{v, @[symbols.tracks], @[symbols.patch]}}
               entries each
                  {x? name, entry} ->
                     if rest == {}:
                        yield entry
                     elif entry.type === "directory":
                        yield all entry.find{rest.join{"/"}}
                  else ->
                     continue
      FileIterator{g{}}

   get{key} =
      @[symbols.register-tracks]{key}
      @_prox{@[symbols.obj].get{key}}

   set{key, value} =
      @[symbols.obj].get-files{}
      @[symbols.register-patch]{key, #update{deprox{value}} &: {type = value}}

   .delete{key} =
      @[symbols.obj].get-files{}
      data = @[symbols.obj]._files[key]
      type = data??.type
      @[symbols.register-patch]{key, #delete{} &: {type = type}}

   gen [^Symbol.iterator]{} =
      @entries{} each {_, v} -> yield v

   keys{} =
      @[symbols.register-tracks]{"//iter"}
      @[symbols.obj].keys{}

   entries{} =
      @[symbols.register-tracks]{"//iter"}
      @[symbols.obj].entries{} each {k, v} ->
         {k, proxy{v, @[symbols.tracks], @[symbols.patch]}}

Object.define-property{FSNodeProxy.prototype, .type} with {
   method get{} =
      @[symbols.register-tracks]{"//type"}
      @[symbols.obj].type
   method set{type} =
      @[symbols.register-patch]{"//type"} with
         #update{type}
}

Object.define-property{FSNodeProxy.prototype, .mtime} with {
   method get{} =
      @[symbols.register-tracks]{"//mtime"}
      @[symbols.obj].mtime
}



class FSNodeTrack:
   gen [^symbols.relevant-changes]{writes} =
      items{@} each {match name, entry} ->
         R"^//:(.*)"! {_, minimatch.make-re! x} ->
            items{writes or {=}} each {wname, _} when x? wname ->
               yield entry
         else ->
            pass
      items{writes or {=}} each match {name, cmd} ->
         when @[name] ->
            yield @[name]
         when cmd.type === .directory and @["//directories"] ->
            yield @["//directories"]
         when @["//iter"] ->
            yield @["//iter"]




register{FileSystem.prototype} with {

   package = require: ..package

   serialize{value} =
      {options = value.options
       directory-cache = value.directory-cache
       root-path = value.root-path}
   create{} =
      Object.create{FileSystem.prototype}
   fill{fs, fields} =
      fs.options = fields.options
      fs.directory-cache = fields.directory-cache
      fs.root-path = fields.root-path

   proxy{deprox! value, tracks, patch} =
      FileSystemProxy{value, tracks, patch}
}


register{FSNode.prototype} with {

   package = require: ..package

   serialize{value} =
      {"//type" => value.type
       "//path" => value.path
       "//fs" => value.fs
       "//mtime" => value.mtime
       "//has-files" => Boolean{value._files}} &:
         object with items{value._files or {=}}
   create{} =
      Object.create{FSNode.prototype}
   fill{fsn, fields} =
      fsn._files = {=}
      items{fields} each {match k, v} ->
         "//type" -> fsn.type = v
         "//path" -> fsn.path = v
         "//fs" -> fsn.fs = v
         "//mtime" -> fsn.mtime = v
         "//has-files" -> fsn._files = if{v, {=}, null}
         R"^//"? -> pass
         else -> fsn._files[k] = v

   proxy{deprox! value, tracks, patch} =
      FSNodeProxy{value.path, value, tracks, patch}

   enter{@, dir} =
      s = dir[symbols.associated-state]
      prox = s.model{@}
      @_watching = true
      @_watcher = chokidar.watch{@path, depth = 0, ignore-initial = true}.on{.all} with
         {match event, fp and p >> path.relative{@path, p}} ->
            ;; do:
            ;;    print {event, @path, p, @_watching}
            when not @_watching ->
               ;; unwatch will not dequeue pending events,
               ;; so we need to use the @_watching flag to ignore them
               return
            match when p === "" ->
               .add ->
                  @_watching = true
                  @_watcher.add{@path}
                  s.transact with -> prox.type = .file
               .add-dir ->
                  @_watching = true
                  @_watcher.add{@path}
                  s.transact with -> prox.type = .directory
               .unlink or .unlink-dir ->
                  @_watcher.unwatch{@path}
                  @_watching = false
                  s.transact with -> prox.type = null
               .change ->
                  @_watching = true
                  @_watcher.add{@path}
                  s.transact with -> prox.touch{}
               else ->
                  print 'Untracked event: {event}'
            when not @_files ->
               return
            .add ->
               s.transact with ->
                  prox.set{p, dir.acquire{FSNode{fp}}}
            .add-dir ->
               s.transact with ->
                  prox.set{p, dir.acquire{FSNode{fp}}}
                  prox.touch-directory{}
            .unlink or .unlink-dir ->
               s.transact with ->
                  prox.delete{p}
            .change ->
               pass
            else ->
               print 'Untracked event: {event}'

   exit{@, dir} =
      if @_watcher:
         @_watcher.unwatch{@path}
}
